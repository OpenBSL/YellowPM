//@skip-check use-non-recommended-method

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
          
    ОтборПоТипу  = 1;
    
    ПрименитьПользовательскиеНастройки();    
    
    СписокФайлов.Параметры.УстановитьЗначениеПараметра("Автор"      , "");
    СписокФайлов.Параметры.УстановитьЗначениеПараметра("Репозиторий", "");
    СписокФайлов.Параметры.УстановитьЗначениеПараметра("ОтборПоТипу", ОтборПоТипу);
    СписокПакетов.Параметры.УстановитьЗначениеПараметра("ОтборПоТипу", ОтборПоТипу);   
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    Если ОбновлятьПриЗапуске Тогда
        
        ОбновитьИндексНаСервере();
        ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", 2);
                    
    КонецЕсли;
    
    СписокПакетовПриАктивизацииСтроки(Неопределено);
    ОтборПоТипуПриИзменении(Неопределено);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоТипуПриИзменении(Элемент)
    
    СписокПакетов.Параметры.УстановитьЗначениеПараметра("ОтборПоТипу", ОтборПоТипу);
    СписокФайлов.Параметры.УстановитьЗначениеПараметра("ОтборПоТипу", ОтборПоТипу);
    
	Если ОтборПоТипу = 1 Тогда           
		
        Элементы.СписокПакетов.КартинкаСтрок = БиблиотекаКартинок.Обработка; 
    	СкрытьПоказатьКнопкиОбработок(Истина);
		
	ИначеЕсли ОтборПоТипу = 2 Тогда            
		
        Элементы.СписокПакетов.КартинкаСтрок = БиблиотекаКартинок.Отчет; 
		СкрытьПоказатьКнопкиОбработок(Истина);
		
	Иначе     
		
        Элементы.СписокПакетов.КартинкаСтрок = БиблиотекаКартинок.ОформлениеКвадратыЗаполненныеТри; 
		СкрытьПоказатьКнопкиОбработок(Ложь);
		
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ОбновлятьПриЗапускеПриИзменении(Элемент)
    ОбновлятьПриЗапускеПриИзмененииНаСервере(ОбновлятьПриЗапуске);
КонецПроцедуры

&НаКлиенте
Процедура СписокПакетовСтраницаРепозиторияНажатие(Элемент, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    ТД_ = Элементы.СписокПакетов.ТекущиеДанные;
    
    Если Не ТД_ = Неопределено Тогда
        ООп = Новый ОписаниеОповещения();
        НачатьЗапускПриложения(ООп, ТД_.СтраницаРепозитория);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура СписокПакетовСтраницаАвтораНажатие(Элемент, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    ТД_ = Элементы.СписокПакетов.ТекущиеДанные;
    
    Если Не ТД_ = Неопределено Тогда
        ООп = Новый ОписаниеОповещения();
        НачатьЗапускПриложения(ООп, ТД_.СтраницаАвтора);
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГруппаРепозиторийПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ТД_ = Элементы.СписокПакетов.ТекущиеДанные;
    
	Если ТД_ <> Неопределено 
		И ТекущаяСтраница.Имя = "ГруппаСтраницаРепозитория" Тогда

		СтраницаРепозитория = ТД_.СтраницаРепозитория;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПакетов

&НаКлиенте
Процедура СписокПакетовПриАктивизацииСтроки(Элемент)
    
    ТД_ = Элементы.СписокПакетов.ТекущиеДанные;
    
    Если ТД_ <> Неопределено Тогда
        
        СписокФайлов.Параметры.УстановитьЗначениеПараметра("Автор", ТД_.Автор);
        СписокФайлов.Параметры.УстановитьЗначениеПараметра("Репозиторий", ТД_.Репозиторий); 
		
		Если Элементы.ГруппаРепозиторий.ТекущаяСтраница.Имя = "ГруппаСтраницаРепозитория" Тогда
			СтраницаРепозитория = ТД_.СтраницаРепозитория;	
		КонецЕсли;
		
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьАдресИндекса(Команда)
    СохранитьАдресИндексаНаСервере(АдресаИндексов.ВыгрузитьЗначения());
КонецПроцедуры

&НаКлиенте
Процедура АдресИндексаПоУмолчанию(Команда)
    АдресаИндексов.Очистить();
    АдресаИндексов.Добавить("https://openyellow.org/data/ypm_index/index.json");
    СохранитьАдресИндексаНаСервере(АдресаИндексов);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИндекс(Команда)
    
    ОбновитьИндексНаСервере();
    ПодключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания", 1);
      
КонецПроцедуры

&НаКлиенте
Процедура Скачать(Команда)
    
    ТД_ = Элементы.СписокФайлов.ТекущиеДанные;
    
    Если Не ТД_ = Неопределено Тогда
        
        ООп    = Новый ОписаниеОповещения("ПослеВыбораПути", ЭтотОбъект, ТД_.АдресФайла);
        Режим  = РежимДиалогаВыбораФайла.Сохранение;                   
        Диалог = Новый ДиалогВыбораФайла(Режим);
        Диалог.ПолноеИмяФайла = ТД_.Файл;
        Диалог.МножественныйВыбор = Ложь;
        Диалог.Показать(ООп);
        
    КонецЕсли;
          
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВДополнительныеОбработки(Команда)
    
    #Если ВебКлиент Тогда
    	Сообщить("Получение файлов недоступно в веб-клиенте!");
    #КонецЕсли
    
    #Если Не ВебКлиент Тогда
    	
    Попытка
		
		ТД_ = Элементы.СписокФайлов.ТекущиеДанные;
		
		Если Не ТД_ = Неопределено Тогда 
			
			ИмяФормыОткрытия  = "Справочник.ДополнительныеОтчетыИОбработки.Форма.ФормаЭлемента";
			ФормаДопОбработок = ПолучитьФорму(ИмяФормыОткрытия);
			ФормаДопОбработок.ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии = Ложь;
			
			Двоичные = YPM_ИнструментыКлиентСервер.Get(ТД_.АдресФайла);
			Адрес    = ПоместитьВоВременноеХранилище(Двоичные, ФормаДопОбработок.УникальныйИдентификатор);
			
			ОписаниеФайла        = Новый Структура("Имя,Хранение", ТД_.Файл, Адрес);
			ПараметрыРегистрации = Новый Структура("АдресДанныхОбработки,Успех", "", Ложь);
			   
	        ФормаДопОбработок.Открыть();
	        ФормаДопОбработок.ОбновитьИзФайлаПослеВыбораФайла(ОписаниеФайла, ПараметрыРегистрации);
			
		КонецЕсли;
        
    Исключение
        
        Сообщить("Ошибка работы со справочником доп. обработок: " + ОписаниеОшибки());
    КонецПопытки;
    
    #КонецЕсли
    
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ОткрытьВнешнийФайл(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлНебезопасно(Команда)
	ОткрытьВнешнийФайл(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРасширение(Команда)
    
    #Если ВебКлиент Тогда
    	Сообщить("Получение файлов недоступно в веб-клиенте!");
    #КонецЕсли
    
    #Если Не ВебКлиент Тогда
    	
    Попытка
		
		ТДФ = Элементы.СписокФайлов.ТекущиеДанные;
        ТДР = Элементы.СписокПакетов.ТекущиеДанные;
		
		Если Не ТДФ = Неопределено Тогда 
			
            UUIDРасширения = УстановитьРасширениеНаСервере(ТДФ.АдресФайла, ТДР.UUID); 
            
            СтруктураЗаписи = Новый Структура;
            СтруктураЗаписи.Вставить("Репозиторий", ТДР.Репозиторий);
            СтруктураЗаписи.Вставить("Автор"      , ТДР.Автор);
            СтруктураЗаписи.Вставить("Файл"       , ТДФ.Файл);
            СтруктураЗаписи.Вставить("Версия"     , ТДФ.Версия);
            СтруктураЗаписи.Вставить("UUID"       , UUIDРасширения);
            
            ЗаписатьУстановку(СтруктураЗаписи);
            
            Элементы.СписокПакетов.Обновить();
            Элементы.СписокФайлов.Обновить();
            
            Сообщить("Установка завершена!");
            
		КонецЕсли;
        
    Исключение
        
        Сообщить("Ошибка добавления расширения: " + ОписаниеОшибки());
    КонецПопытки;
    
    #КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура УдалитьРасширение(Команда)
    
    ТДФ = Элементы.СписокФайлов.ТекущиеДанные;
    
    Если ТДФ = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    СообщениеУдаления = УдалитьРасширениеНаСервере(ТДФ.UUID);
    Сообщить(СообщениеУдаления);
    
    Элементы.СписокПакетов.Обновить();
    Элементы.СписокФайлов.Обновить();
              
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьРасширениеКакУстановленное(Команда)
    
    ООп = Новый ОписаниеОповещения("ПослеВводаИмениРасширения", ЭтотОбъект);
    ПоказатьВводСтроки(ООп, , "Имя расширения из списка расширений конфигурации", , Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьАдресИндексаНаСервере(Знач АдресИндекса)
    
    КлючОбъекта  = "YPM";
    КлючНастроек = "АдресИндекса";

    ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, АдресИндекса);
    
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновлятьПриЗапускеПриИзмененииНаСервере(Знач Значение)
    ХранилищеОбщихНастроек.Сохранить("YPM", "ОбновлятьПриЗапуске", Значение);
КонецПроцедуры

&НаСервере
Процедура ОбновитьИндексНаСервере()
    
    КлючХранилища = ПоместитьВоВременноеХранилище("", УникальныйИдентификатор);
    
    Элементы.ГруппаЛево.Видимость       = Ложь;
    Элементы.ГруппаПраво.Видимость      = Ложь;
    Элементы.ГруппаЗагрузка.Видимость   = Истина;
    Элементы.ГруппаСтраницы.Доступность = Ложь;
    
    Адреса = АдресаИндексов.ВыгрузитьЗначения();
       
    МассивПараметров = Новый Массив;
    МассивПараметров.Добавить(Адреса);
    МассивПараметров.Добавить(КлючХранилища);
    
    ФЗД = ФоновыеЗадания.Выполнить("YPM_Основные.ОбновитьИндекс", МассивПараметров, УникальныйИдентификатор);
    
    ИДФоновогоЗадания = ФЗД.УникальныйИдентификатор;
    
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнениеФоновогоЗадания() Экспорт
    
    Если ПроверитьВыполнениеФоновогоЗаданияСервер() Тогда
        
        СтруктураОбновления = ПолучитьИзВременногоХранилища(КлючХранилища);
		
		Попытка
        	ЗаписатьЛогНаСервере(СтруктураОбновления["Лог"]);  
			ОткрытьФормуОбновления(СтруктураОбновления);
		Исключение                   
			Сообщить("Не удалось обновить индекс! Возможно, для расширения установлен безопасный режим");
		КонецПопытки;
		
        
        Элементы.ГруппаЛево.Видимость       = Истина;
        Элементы.ГруппаПраво.Видимость      = Истина;
        Элементы.ГруппаЗагрузка.Видимость   = Ложь;
        Элементы.ГруппаСтраницы.Доступность = Истина;
        
        ОтключитьОбработчикОжидания("ПроверитьВыполнениеФоновогоЗадания");
        
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЛогНаСервере(Знач Лог)
	Объект.ЛогОбновления = Лог;	
КонецПроцедуры

&НаСервере
Функция ПроверитьВыполнениеФоновогоЗаданияСервер() 
    
    Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИДФоновогоЗадания); 
    
    Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
        Возврат Ложь;
    Иначе
        Возврат Истина;
    КонецЕсли;
    
КонецФункции

&НаКлиенте
Процедура ПослеВыбораПути(Результат, АдресФайла) Экспорт
	
    #Если ВебКлиент Тогда
    	Сообщить("Получение файлов недоступно в веб-клиенте!");
    #КонецЕсли
    
    #Если Не ВебКлиент Тогда
    	
	Попытка
		Если Не Результат = Неопределено Тогда
			
			YPM_ИнструментыКлиентСервер.Get(АдресФайла, Результат[0]);
			Сообщить("Файл успешно загружен");
			
		КонецЕсли;
		
	Исключение
		Сообщить("Ошибка при загрузке файла: " + ОписаниеОшибки());	
	КонецПопытки;
	
	#КонецЕсли
    
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнешнийФайл(Знач БезопасныйРежим)

	Попытка
		
		ТД_ = Элементы.СписокФайлов.ТекущиеДанные;
		
		Если Не ТД_ = Неопределено Тогда 
			
			ИмяФормыОбработки = ПодключитьВнешнююОбработкуНаСервере(ТД_.АдресФайла, ТД_.Тип, БезопасныйРежим);
			
			Если Не ЗначениеЗаполнено(ИмяФормыОбработки) Тогда
				Возврат;
			КонецЕсли;
			
			ОткрытьФорму(ИмяФормыОбработки);
			
		КонецЕсли;
		
	Исключение
		
		Сообщить("Ошибка открытия обработки: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьПользовательскиеНастройки()
    
    КлючОбъекта    = "YPM";
    МассивИндексов = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "АдресИндекса");
    Обновлять      = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "ОбновлятьПриЗапуске");
    ТокенGithub    = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, "ТокенGithub");
    
    Если ЗначениеЗаполнено(МассивИндексов) И ТипЗнч(МассивИндексов) = Тип("Массив") Тогда
        
        АдресаИндексов.ЗагрузитьЗначения(МассивИндексов);
        
    Иначе
        
        АдресаИндексов.Добавить("https://openyellow.org/data/ypm_index/index.json");
        ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, "АдресИндекса", АдресаИндексов.ВыгрузитьЗначения());
        
    КонецЕсли;

    ОбновлятьПриЗапуске = ?(Обновлять = Неопределено, Истина, Обновлять);
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодключитьВнешнююОбработкуНаСервере(Знач Адрес, Знач Тип, Знач БезопасныйРежим)

	Двоичные = YPM_ИнструментыКлиентСервер.Get(Адрес);
	Адрес    = ПоместитьВоВременноеХранилище(Двоичные);
	
	Если Тип = 1 Тогда  
		
		ВнешняяОбработка = ВнешниеОбработки.Подключить(Адрес, , БезопасныйРежим);
		ИмяФормы         = "ВнешняяОбработка." + ВнешняяОбработка; 	
		
	ИначеЕсли Тип = 2 Тогда  
		
		ВнешнийОтчет = ВнешниеОтчеты.Подключить(Адрес, , БезопасныйРежим);
		ИмяФормы     = "ВнешнийОтчет." + ВнешнийОтчет; 
		
	Иначе
		Сообщить("Файл не является отчетом или обработкой"); 
		Возврат "";
	КонецЕсли;

	ИмяФормы = ИмяФормы + ".Форма";
	
	Возврат ИмяФормы;
	
КонецФункции

&НаКлиенте
Процедура СкрытьПоказатьКнопкиОбработок(Знач ВидимостьЭлемента)
	
	Элементы.ОткрытьФайл.Видимость                      = ВидимостьЭлемента;
	Элементы.ОткрытьФайлНебезопасно.Видимость           = ВидимостьЭлемента;
	Элементы.ДобавитьВДополнительныеОбработки.Видимость = ВидимостьЭлемента;
    
    Элементы.УстановитьРасширение.Видимость               = Не ВидимостьЭлемента;
    Элементы.УдалитьРасширение.Видимость                  = Не ВидимостьЭлемента;
    Элементы.ОтметитьРасширениеКакУстановленное.Видимость = Не ВидимостьЭлемента;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОбновления(Знач СтруктураВерсии)
    
    Если СтруктураВерсии["Обновление"] Тогда
        
       ООп = Новый ОписаниеОповещения("ПослеЗакрытияОбновления", ЭтотОбъект);
       ОткрытьФорму("Обработка.YPM_МенеджерПакетов.Форма.ФормаОбновления", СтруктураВерсии, , , , , ООп);
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияОбновления(Результат, ДополнительныеПараметры) Экспорт
    
    Если ЗначениеЗаполнено(Результат) Тогда
        Сообщить(Результат);
    КонецЕсли;
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьРасширениеНаСервере(Знач АдресФайла, Знач UUID)
    
    Двоичные = YPM_ИнструментыКлиентСервер.Get(АдресФайла);
    
    Если ЗначениеЗаполнено(UUID) Тогда
        
        UUID = Новый УникальныйИдентификатор(UUID);
        
        СтруктураОтбора = Новый Структура("УникальныйИдентификатор", UUID);
        Найденные       = РасширенияКонфигурации.Получить(СтруктураОтбора);
        
        Если Найденные.Количество() = 0 Тогда
            НовоеРасширение = РасширенияКонфигурации.Создать();
        Иначе	
            НовоеРасширение = Найденные[0];
        КонецЕсли;   
        
    Иначе
       НовоеРасширение = РасширенияКонфигурации.Создать(); 
    КонецЕсли;
    
    НовоеРасширение.БезопасныйРежим = Ложь;
    НовоеРасширение.Записать(Двоичные);
    
    Возврат Строка(НовоеРасширение.УникальныйИдентификатор);
            
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьУстановку(Знач СтруктураЗаписи)
    
    МЗУ = РегистрыСведений.YPM_УстановленныеРешения.СоздатьМенеджерЗаписи();
    ЗаполнитьЗначенияСвойств(МЗУ, СтруктураЗаписи);
    МЗУ.Записать(Истина);
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция УдалитьРасширениеНаСервере(Знач UUID)
    
    СообщениеУдаления = "Расширение для удаления не найдено! Возможно, оно уже было удалено";
    
    Если ЗначениеЗаполнено(UUID) Тогда
        
        Попытка
            
            ОтборРасширения     = Новый Структура("УникальныйИдентификатор", Новый УникальныйИдентификатор(UUID));
            УдаляемоеРасширение = РасширенияКонфигурации.Получить(ОтборРасширения);
            
            Если УдаляемоеРасширение.Количество() <> 0 Тогда
                УдаляемоеРасширение = УдаляемоеРасширение[0];
                УдаляемоеРасширение.Удалить();
                СообщениеУдаления = "Расширение удалено успешно!";
            КонецЕсли;
            
        Исключение
            СообщениеУдаления = "Не удалось удалить расширение: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        КонецПопытки;
        
    КонецЕсли;
    
    YPM_Основные.ОбновитьСписокУстановленныхРасширений();
    
    Возврат СообщениеУдаления;

КонецФункции

&НаКлиенте
Процедура ПослеВводаИмениРасширения(Результат, ДопПараметры) Экспорт
    
    Если Не ЗначениеЗаполнено(Результат) Тогда
        Возврат;
    КонецЕсли;
    
    Попытка
		
		ТДФ = Элементы.СписокФайлов.ТекущиеДанные;
        ТДР = Элементы.СписокПакетов.ТекущиеДанные;
		
        Если Не ТДФ = Неопределено Тогда 
            
            СтруктураЗаписи = Новый Структура;
            СтруктураЗаписи.Вставить("Репозиторий", ТДР.Репозиторий);
            СтруктураЗаписи.Вставить("Автор"      , ТДР.Автор);
            СтруктураЗаписи.Вставить("Файл"       , ТДФ.Файл);
            СтруктураЗаписи.Вставить("Версия"     , ТДФ.Версия);

            СообщениеОтметки = ОтметитьРасширениеНаСервере(СтруктураЗаписи, Результат);
            
            Элементы.СписокПакетов.Обновить();
            Элементы.СписокФайлов.Обновить();

		КонецЕсли;
        
    Исключение
        
        СообщениеОтметки = "Ошибка добавления расширения: " + ОписаниеОшибки();
        
    КонецПопытки;
    
    Сообщить(СообщениеОтметки);
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтметитьРасширениеНаСервере(СтруктураЗаписи, Результат)
    
    ОтборРасширения = Новый Структура("Имя", СокрЛП(Результат));
    ОтмеченноеРасширение = РасширенияКонфигурации.Получить(ОтборРасширения);
    
    Если ОтмеченноеРасширение.Количество() = 0 Тогда
        Возврат "Установленное расширение с указанным именем не найдено!";
    Иначе
        ОтмеченноеРасширение = Строка(ОтмеченноеРасширение[0].УникальныйИдентификатор);
    КонецЕсли;
    
    СтруктураЗаписи.Вставить("UUID", ОтмеченноеРасширение);
    
    ЗаписатьУстановку(СтруктураЗаписи);
      
    Возврат "Расширение отмечено как установленное";
    
КонецФункции

#КонецОбласти
